// Any copyright is dedicated to the Public Domain.
// http://creativecommons.org/publicdomain/zero/1.0/

// oldC2SCases/unfolding_old_heap.chalice

field x: Int

predicate P(this: Ref) {
  acc(this.x)
}

method fail10(this: Ref)
  requires acc(P(this), write)
{
  //:: ExpectedOutput(exhale.failed:insufficient.permission)
  exhale acc(P(this), write) && unfolding P(this) in this.x == old(this.x)
}

method success10(this: Ref)
  requires acc(P(this), write)
{
  exhale acc(P(this), write) && unfolding P(this) in this.x == old(unfolding P(this) in this.x)
}

method success2(this: Ref)
  requires acc(this.x)
{
  fold P(this)
  exhale acc(P(this), write) && unfolding P(this) in this.x == old(this.x)
}

method fail5(this: Ref)
  requires acc(P(this), write)
{
  unfold P(this)
  this.x := this.x + 1
  fold P(this)
  //:: ExpectedOutput(exhale.failed:insufficient.permission)
  exhale acc(P(this), write) && unfolding P(this) in this.x == old(this.x) + 1
}

method success5(this: Ref)
  requires this != null
  requires acc(P(this), write)
{
  unfold acc(P(this), write)
  this.x := this.x + 1
  fold acc(P(this), write)
  exhale acc(P(this), write) && (unfolding acc(P(this), write) in this.x == old((unfolding acc(P(this), write) in this.x)) + 1)
}

method success4(this: Ref)
  requires this != null
  requires acc(this.x, write)
{
  this.x := this.x + 1
  fold acc(P(this), write)
  exhale acc(P(this), write) && (unfolding acc(P(this), write) in this.x == old(this.x) + 1)
}

method fail1(this: Ref)
  requires this != null
  requires acc(this.x, write)
{
  fold acc(P(this), write)
  //:: ExpectedOutput(exhale.failed:insufficient.permission)
  exhale  acc(P(this), write) && (unfolding acc(P(this), write) in this.x == old((unfolding acc(P(this), write) in this.x)))
}

method fail2(this: Ref)
  requires this != null
  requires acc(this.x, write)
{
  this.x := 0
  fold acc(P(this), write)
  //:: ExpectedOutput(exhale.failed:assertion.false)
  exhale acc(P(this), write) && (unfolding acc(P(this), write) in this.x == old(this.x))
}
