// Any copyright is dedicated to the Public Domain.
// http://creativecommons.org/publicdomain/zero/1.0/

field f: Int

function fun_f(xs: Seq[Ref], j: Int): Int
  requires forall i: Int :: 0 <= i && i < |xs| ==> acc(xs[i].f)
  requires 0 <= j && j < |xs|
{ get_f(xs[j]) }

function get_f(x: Ref): Int
  requires acc(x.f)
{ x.f }

method test_f() {
  var ys: Seq[Ref]
  var k: Int
  assume 0 <= k && k < |ys|

  inhale forall i: Int :: 0 <= i && i < |ys| ==> acc(ys[i].f)
  inhale fun_f(ys, k) == 0
  exhale get_f(ys[k]) == 0

  label l1

  ys[k].f := ys[k].f - 1

  assert get_f(ys[k]) < old[l1](fun_f(ys, k))
  assert forall j: Int :: 0 <= j && j < |ys| && j != k ==> get_f(ys[j]) == old[l1](fun_f(ys, j))
}



predicate P(x: Ref) { acc(x.f) }

function fun_P(xs: Seq[Ref], j: Int): Int
  requires forall i: Int :: 0 <= i && i < |xs| ==> P(xs[i])
  requires 0 <= j && j < |xs|
{ get_P(xs[j]) }

function get_P(x: Ref): Int
  requires P(x)
{ unfolding P(x) in get_f(x) }

method test_P() {
  var ys: Seq[Ref]
  var k: Int
  assume 0 <= k && k < |ys|

  inhale forall i: Int :: 0 <= i && i < |ys| ==> P(ys[i])
  inhale fun_P(ys, k) == 0
  exhale get_P(ys[k]) == 0

  label l1

  unfold P(ys[k])
  ys[k].f := ys[k].f - 1
  fold P(ys[k])

  assert get_P(ys[k]) < old[l1](fun_P(ys, k))
  assert forall j: Int :: 0 <= j && j < |ys| && j != k ==> get_P(ys[j]) == old[l1](fun_P(ys, j))
}



